;『;』はコメントアウトの意味
;変数は【】で記載
;パターン1 ログイン情報をttlファイル内に記載する場合

;=======================================================
;ログイン情報をここにまとめて記載
HOSTADDR = '192.168.1.1'
USERNAME = 'will'
PASSWORD = 'of'
ENABLEPASS = 'work'
;PROMPTは環境に合わせて変更。以下の場合、RT1>、RT1# 等に対応
PROMPT = '.+[>#]'
;=======================================================

;ログイン用コマンドをstrconcatで組み立てる。
;COMMAND という変数に'ssh://'という文字列を代入する
COMMAND = 'ssh://'

;strconcat A B でAにBを付け加えるという意味
;以下の場合、上で定義したssh://という文字列にwillを付け加えている
strconcat COMMAND USERNAME

;以降も同様にCOMMANDという変数に文字列を付け加えていく
strconcat COMMAND '@'
strconcat COMMAND HOSTADDR
strconcat COMMAND ' /auth=password /passwd='
strconcat COMMAND PASSWORD

;strconcatにより、COMMANDはssh://will@192.168.1.1 /auth=password /passwd=of
;という文字列となる。以下のconnectで192.168.1.1へssh接続を実行
connect COMMAND

;ログイン後のRT1>、RT1#といったプロンプトを待つ
waitregex PROMPT

;sendlnでteratermに投入したいコマンドを入力可能。
;以下の場合、Enterを一回実行の意味
sendln ''

;ログイン後のプロンプトにより、打ち込むコマンドを変える
wait '>' '#'

;『sendln ''』実行時に『>』が帰ってきたら、result=1 以下の処理を実行
if result=1 then
        sendln 'enable'
        wait 'Password:'

       ;事前に定義したENABLEPASSを投入
        sendln ENABLEPASS
        wait '#'

       ;ter len 0が必要であれば投入
        sendln 'ter len 0'
        wait '#'

elseif result=2 then
        sendln 'ter len 0'
        wait '#'
endif

;これでログイン完了。この後ろにログファイル起動のコードを追加したりもします。
;ログファイル起動のコードは https://github.com/kentarou-okada/teraterm/logopen を参照して下さい
